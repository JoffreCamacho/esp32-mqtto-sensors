//For ATRIUM CDR with love by Joffre Camacho - 2023

#include <WiFi.h>
#include <ESPmDNS.h>
#include <WiFiUdp.h>
#include <ArduinoOTA.h>
#include <PubSubClient.h>
#include <OneWire.h>
#include <DallasTemperature.h>
#include <ArduinoJson.h>

#define RELAY2_PIN 25 // Light Relay control pin
#define ONE_WIRE_BUS 26 // DS18B20 pin
#define RELAY_PIN 27 // Pump Relay control pin
#define TURBIDITY_SENSOR_PIN 32 // Turbidity sensor pin
#define MAX_SENSOR_VALUE 4095 // max value for a 12-bit ADC
#define RELAY3_PIN 33 // Heater Relay control pin
#define LIGHT_SENSOR_PIN 34 // LX1972 light sensor pin
#define TDS_SENSOR_PIN 35 // TDS_Sensor PIN
#define CO2_SENSOR_PIN 36 // MS1100 CO2 sensor pin
#define PH_SENSOR_PIN 39 // PH-014 sensor pin


OneWire oneWire(ONE_WIRE_BUS);
DallasTemperature sensors(&oneWire);

const char* ssid = "YOUR SSID";
const char* password = "YOUR PASSWORD";
const char* mqttServer = "MQTT SERVER IP";
const int mqttPort = MQTT SERVER PORT;

WiFiClient wifiClient;
PubSubClient mqttClient(wifiClient);

bool relayState = false; // Variable to store the state of the relay
bool relay2State = false; // Variable to store the state of the second relay
bool relay3State = false; // Variable to store the state of the third relay


void setup() {
  Serial.begin(115200);
  Serial.println("Booting");

  WiFi.mode(WIFI_STA);
  WiFi.begin(ssid, password);
  
  while (WiFi.waitForConnectResult() != WL_CONNECTED) {
    Serial.println("Connection Failed! Rebooting...");
    delay(5000);
    ESP.restart();
  }

  ArduinoOTA
    .onStart([]() {
      String type;
      if (ArduinoOTA.getCommand() == U_FLASH)
        type = "sketch";
      else 
        type = "filesystem";
      Serial.println("Start updating " + type);
    })
    .onEnd([]() {
      Serial.println("\nEnd");
    })
    .onProgress([](unsigned int progress, unsigned int total) {
      Serial.printf("Progress: %u%%\r", (progress / (total / 100)));
    })
    .onError([](ota_error_t error) {
      Serial.printf("Error[%u]: ", error);
      if (error == OTA_AUTH_ERROR) Serial.println("Auth Failed");
      else if (error == OTA_BEGIN_ERROR) Serial.println("Begin Failed");
      else if (error == OTA_CONNECT_ERROR) Serial.println("Connect Failed");
      else if (error == OTA_RECEIVE_ERROR) Serial.println("Receive Failed");
      else if (error == OTA_END_ERROR) Serial.println("End Failed");
    });

  ArduinoOTA.begin();

  Serial.println("Ready");
  Serial.print("IP address: ");
  Serial.println(WiFi.localIP());

  mqttClient.setServer(mqttServer, mqttPort);
  mqttClient.setCallback(mqttCallback);

  sensors.begin(); // Initialize DS18B20 sensor
  pinMode(LIGHT_SENSOR_PIN, INPUT); // Initialize LX1972 light sensor
  pinMode(RELAY_PIN, OUTPUT); // Set relay pin as output
  pinMode(RELAY2_PIN, OUTPUT); // Set second relay pin as output
  pinMode(RELAY3_PIN, OUTPUT); // Set third relay pin as output
  analogReadResolution(12); // ESP32 has 12-bit resolution
  pinMode(CO2_SENSOR_PIN, INPUT); // Initialize CO2 sensor
  pinMode(PH_SENSOR_PIN, INPUT); // Initialize PH sensor


}

void mqttCallback(char* topic, byte* payload, unsigned int length) {
  Serial.println("Entered mqttCallback");
  String message = "";
  for (int i = 0; i < length; i++) {
    message += (char)payload[i];
  }

  Serial.println("Message: " + message);

  // Depending on the topic, control the corresponding relay
  if (String(topic) == "esp32/relay1") {
    if (message == "on") {
      digitalWrite(RELAY_PIN, HIGH);
      relayState = true;
      Serial.println("Relay 1 turned on");
    } else if (message == "off") {
      digitalWrite(RELAY_PIN, LOW);
      relayState = false;
      Serial.println("Relay 1 turned off");
    }
  } else if (String(topic) == "esp32/relay2") {
    if (message == "on") {
      digitalWrite(RELAY2_PIN, HIGH);
      relay2State = true;
      Serial.println("Relay 2 turned on");
    } else if (message == "off") {
      digitalWrite(RELAY2_PIN, LOW);
      relay2State = false;
      Serial.println("Relay 2 turned off");
    }
  } else if (String(topic) == "esp32/relay3") {
    if (message == "on") {
      digitalWrite(RELAY3_PIN, HIGH);
      relay3State = true;
      Serial.println("Relay 3 turned on");
    } else if (message == "off") {
      digitalWrite(RELAY3_PIN, LOW);
      relay3State = false;
      Serial.println("Relay 3 turned off");
    }
  }
}

void connectToMqtt() {
  Serial.println("Entered connectToMqtt");
  while (!mqttClient.connected()) {
    Serial.println("Connecting to MQTT server...");
    if (mqttClient.connect("ESP32Client")) {
      Serial.println("Connected to MQTT server");
      mqttClient.subscribe("esp32/relay1");
      mqttClient.subscribe("esp32/relay2");
      mqttClient.subscribe("esp32/relay3");
    } else {
      Serial.print("Failed to connect to MQTT server, rc=");
      Serial.print(mqttClient.state());
      Serial.println(" Retrying in 5 seconds...");
      delay(5000);
    }
  }
}

void loop() {
  Serial.print("Free memory: ");
  Serial.println(ESP.getFreeHeap());
  if (!mqttClient.connected()) {
    connectToMqtt();
  }
  mqttClient.loop();
  ArduinoOTA.handle();

  // Read temperature
  sensors.requestTemperatures();
  float temperatureC = sensors.getTempCByIndex(0);

  // Read light intensity
  int lightIntensity = analogRead(LIGHT_SENSOR_PIN);

  // Get ESP32 MAC address
  String macAddress = WiFi.macAddress();

  // Get Turbidity
  int sensorValue = analogRead(TURBIDITY_SENSOR_PIN);
  int turbidity = MAX_SENSOR_VALUE - sensorValue; // invert the reading
  
  // Get TDS
  int tdsValue = analogRead(TDS_SENSOR_PIN);

  // Read CO2 sensor value
  int CO2 = analogRead(CO2_SENSOR_PIN); 

  // Read PH sensor value
  int phValue = analogRead(PH_SENSOR_PIN); 
  
  // Create JSON document
  StaticJsonDocument<200> doc;
  doc["macAddress"] = macAddress;
  doc["temperatureC"] = temperatureC;
  doc["lightIntensity"] = lightIntensity;
  doc["turbidity"]= turbidity;
  doc["tds"] = tdsValue;
  doc["CO2"] = CO2; 
  doc["phValue"] = phValue; // Include PH value in JSON
  doc["relayState"] = relayState; // Include relay state in JSON
  doc["relay2State"] = relay2State; // Include second relay state in JSON
  doc["relay3State"] = relay3State; // Include third relay state in JSON

  //Se puede actualizar el codigo via Wifi al ESP32
  
  char jsonBuffer[512];
  serializeJson(doc, jsonBuffer); // Serialize the JSON document

  mqttClient.publish("esp32/sensorData", jsonBuffer);
  Serial.print("Temperature: ");
  Serial.print(temperatureC);
  Serial.println(" C");
  Serial.print("Light Intensity: ");
  Serial.println(lightIntensity);
  Serial.print("Sensor Value: ");
  Serial.println(turbidity);
  Serial.print("TDS Value: ");
  Serial.println(tdsValue);
  Serial.print("MAC Address: ");
  Serial.println(macAddress);
  Serial.print("CO2 Value: ");
  Serial.println(CO2);
  Serial.print("PH Value: ");
  Serial.println(phValue);
  Serial.print("Relay State: ");
  Serial.println(relayState);
  Serial.print("Relay 2 State: ");
  Serial.println(relay2State);
  Serial.print("Relay 3 State: ");
  Serial.println(relay3State);

  delay(2000); // Delay for 2 seconds (must not be higher that 5 or OTA will not work)
}
